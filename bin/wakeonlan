#!/usr/bin/perl

use FindBin qw($Bin);
use lib "./extlib/lib/perl5/";
use Mojolicious::Lite;
use Mojo::File 'path', 'tempfile';
use IPC::Run3 'run3';

app->mode('production');
app->secrets( ['HTJVYGBtLzzo7vOb3Xg3'] );

my $config = {
    allowed_groups => ['abt_edv'],
    ldap           => {
        host    => 'ldap.hal.taz,de',
        user_dn => 'uid=%s,ou=users,dc=taz,dc=de',
    },
};

patch '/update' => sub {
    my ( $stdout, $stderr );
    run3( [ '/usr/sbin/arp', '-a' ], \undef, \$stdout, \$stderr );

    my %macs;

    for my $line ( split( "\n", $stdout ) ) {
        next if $line =~ /^\?/;
        my @fields = split( ' ', $line );
        next unless $fields[3] =~ /:/;
        $macs{ $fields[0] } = $fields[3];
    }

    my $database = path('macs.txt');

    if ( -e $database ) {
        my $fh = $database->open('<');
        while (<$fh>) {
            my @fields = split;
            $macs{ $fields[0] } = $fields[1];
        }
    }

    my $tempfile = tempfile();
    my $fh       = $tempfile->open('>');
    for my $name ( keys %macs ) {
        say $fh $name . ' ' . $macs{$name};
    }
    $fh->close;
    $tempfile->move_to( $database->to_string );
    shift->render( data => '', status => 201 );
};

get '/' => sub {
    my $c = shift;
    $c->stash(
        lines => [
            map { [split] } split( "\n", path('macs.txt')->slurp )
        ]
    );
    $c->render;
} => 'index';

post '/wake-up/:mac' => sub {
    my $c = shift;
    my $output;
    run3 [ '/usr/bin/wakeonlan', $c->stash('mac') ], \undef, \$output, \$output;
    $c->flash( output => $output );
    $c->redirect_to('index');
} => 'wake_up';

app->start;

__DATA__

@@ style.css

th {
  text-align: left;
}

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
<head>
	<meta charset="UTF-8">
	%= stylesheet 'style.css';
	<title>Wake-on-LAN</title>
</head>
<body>
	<h1>Wake-on-LAN</h1>
	<%= content %>
</body>
</html>

@@ index.html.ep
% layout 'default';

% if ( my $output = flash('output') ) {
<div><pre><%= $output %> <pre></div>
% }

<table>
    <thead>
        <tr>
            <th>Hostname</th>
            <th>MAC-Address</th>
        </tr>
    </thead>
    <tbody>
% for my $line ( @$lines ) {
        <tr>
            <td><%= $line->[0] %></td>
            <td><%= $line->[1] %></td>
            <td><%= button_to 'wake up' => 'wake_up' => { mac => $line->[1] }  %></td>
        </tr>
% }
    </tbody>
</table>
