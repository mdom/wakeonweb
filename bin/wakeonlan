#!/usr/bin/perl

use FindBin qw($Bin);
use lib "./extlib/lib/perl5/";
use Mojolicious::Lite;
use Mojo::File 'path';
use IPC::Run3 'run3';

app->mode('production');
app->secrets( ['HTJVYGBtLzzo7vOb3Xg3'] );

my $config = {
    allowed_groups => ['abt_edv'],
    ldap           => {
        host    => 'ldap.hal.taz,de',
        user_dn => 'uid=%s,ou=users,dc=taz,dc=de',
    },
};

get '/' => sub {
    my $c = shift;
    $c->stash(
        lines => [
            map { [split] } split( "\n", path('/var/www/hal/macs.txt')->slurp )
        ]
    );
    $c->render;
} => 'index';

post '/wake-up/:mac' => sub {
    my $c = shift;
    my $output;
    run3 [ '/usr/bin/wakeonlan', $c->stash('mac') ], \undef, \$output, \$output;
    $c->flash( output => $output );
    $c->redirect_to('index');
} => 'wake_up';

# Start the Mojolicious command system
app->start;

__DATA__

@@ style.css

th {
  text-align: left;
}

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
<head>
	<meta charset="UTF-8">
	%= stylesheet 'style.css';
	<title>Wake-on-LAN</title>
</head>
<body>
	<h1>Wake-on-LAN</h1>
	<%= content %>
</body>
</html>

@@ index.html.ep
% layout 'default';

% if ( my $output = flash('output') ) {
<div><pre><%= $output %> <pre></div>
% }

<table>
    <thead>
        <tr>
            <th>Hostname</th>
            <th>MAC-Address</th>
        </tr>
    </thead>
    <tbody>
% for my $line ( @$lines ) {
        <tr>
            <td><%= $line->[0] %></td>
            <td><%= $line->[1] %></td>
            <td><%= button_to 'wake up' => 'wake_up' => { mac => $line->[1] }  %></td>
        </tr>
% }
    </tbody>
</table>
